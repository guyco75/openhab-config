
var Timer boiler_timer = null

rule "Boiler System Start"
  when
    System started
  then
    boiler_sw.sendCommand(OFF);
    boiler_p.postUpdate(0);
    boiler_h.postUpdate(17);
    boiler_m.postUpdate(0);
    boiler_d.postUpdate(120);
end

rule "boiler_sw > boiler_p"
  when
    Item boiler_sw received update
  then
    if (boiler_sw.state == ON) {
      boiler_p.postUpdate(1)
    } else {
      boiler_p.postUpdate(0)
    }
end

//TODO: use "received command" and triggeringItem when moving to a newer OH version
rule "Boiler settings update"
  when
    Item boiler_p received update or
    Item boiler_h received update or
    Item boiler_m received update or
    Item boiler_d received update
  then
    if (boiler_timer != null) {
        logInfo("Boiler", "removing timer")
        boiler_timer.cancel
        boiler_timer = null
    }

    switch (boiler_p.state) {
      case 0:
	    boiler_sw.sendCommand(OFF)

      case 1:
	    boiler_sw.sendCommand(ON)

      case 2: {
        logInfo("Boiler", "Set Boiler Timer")
        var h = (boiler_h.state as DecimalType).intValue
        var m = (boiler_m.state as DecimalType).intValue
        var d = (boiler_d.state as DecimalType).intValue

        var start_time = h*60 + m
        var end_time = start_time + d
        var nowMin = now.getMinuteOfDay

        if (end_time < start_time)
          end_time += 24*60

        if (start_time <= nowMin && nowMin < end_time) {
          logInfo("Boiler", "TIMER: ON  |  start_time="+start_time + "  end_time="+end_time + "  nowMin="+nowMin)
          boiler_sw.sendCommand(ON)
          var t=now.plusMinutes(end_time-nowMin).withSecondOfMinute(0).withMillisOfSecond(0)
          boiler_timer = createTimer(t) [|
            boiler_p.postUpdate(0)
          ]
        } else {
          boiler_sw.sendCommand(OFF)
          if (start_time < nowMin)
            start_time += 24*60
          logInfo("Boiler", "TIMER: WAIT  |  start_time="+start_time + "  end_time="+end_time + "  nowMin="+nowMin)
          var t=now.plusMinutes(start_time-nowMin).withSecondOfMinute(0).withMillisOfSecond(0)
          boiler_timer = createTimer(t) [|
            boiler_p.postUpdate(2) /// JUST TRIGGER WITHOUT 2!!
          ]
        }
      }
    }
end

