rule "Voice control"
when
  Item VoiceCommand received command
then
  var String command = receivedCommand.toString.toLowerCase
  var String cmd = null
  var String set_val_str = null
  var Integer set_val_num = 0

  if (command.startsWith("turn")) {
    if (command.contains(" on ") || command.endsWith(" on")) {
      cmd = "ON"
    } else if (command.contains(" off ") || command.endsWith(" off")) {
      cmd = "OFF"
    }
  }

  if (command.startsWith("open ")) {
    cmd = "OPEN"
  }

  if (command.startsWith("close ")) {
    cmd = "CLOSE"
  }

  set_val_str = transform("REGEX", "^set .+ to ([a-z0-9 ]+)", command)
  if (set_val_str != null) {
    var String s = transform("REGEX", "^([0-9]+)$", set_val_str)
    if (s != null) {
      set_val_num = Integer::parseInt(s)
      cmd = "SET_NUM"
    } else {
      cmd = "SET_STR"
    }
  }

  logInfo("voice", command + " | cmd=" + cmd + " | set_val_str=" + set_val_str);

  if (command.contains("air condition")) {
    switch (cmd) {
      case "OFF": {
        if (ac_state.state == 1) {
          ac_power.sendCommand(ON) //used both for power on and power off
        }
      }
      case "ON": {
        if (ac_state.state == 0) {
          ac_power.sendCommand(ON)
          Thread::sleep(2000)
          ac_i_feel.sendCommand(ON)
        }
      }
      case "SET_NUM": ac_temp.sendCommand(set_val_num)
    }
  }

  if (command.contains("kitchen light")) {
    switch (cmd) {
      case "OFF"    : kitchen_light.sendCommand(0)
      case "ON"     : kitchen_light.sendCommand(100)
      case "SET_NUM": kitchen_light.sendCommand(set_val_num)
    }
  }

  if (command.contains("boiler")) {
    switch (cmd) {
      case "OFF": boiler_p.sendCommand(0)
      case "ON" : boiler_p.sendCommand(1)
    }
  }

  if (command.contains("bathroom heater")) {
    switch (cmd) {
      case "OFF": bathroom_heater.sendCommand(OFF)
      case "ON" : bathroom_heater.sendCommand(ON)
    }
  }

  if (command.contains("media center") || command.contains("mediacenter")) {
    switch (cmd) {
      case "OFF"    : livingroom_mediacenter.sendCommand("off")
      case "SET_STR": {
        if (set_val_str.contains("tv")) {
          livingroom_mediacenter.sendCommand("idan_plus")
        } else if (set_val_str.contains("pc") || set_val_str.contains("computer")) {
          livingroom_mediacenter.sendCommand("pc")
        }
      }
    }
  }

  if (command.contains("roller shutter")) {
    switch (cmd) {
      case "OPEN"   : rollershutter_livingroom.sendCommand(0)
      case "CLOSE"  : rollershutter_livingroom.sendCommand(100)
      case "SET_NUM": rollershutter_livingroom.sendCommand(set_val_num)
    }
  }
end
